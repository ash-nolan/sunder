namespace std;
import "std/result.sunder";

struct writer_vtable {
    ### func write(self: *any, bytes: []byte) std::result[[:usize, :[]byte]]
    ###
    ### Attempt to write `countof(bytes)` with the provided writer.
    ###
    ### On success this function returns the number of bytes written, which may
    ### be less than `countof(bytes)` in the event of a partial write.
    ###
    ### On error this function returns a static string describing error.
    var write: func(*any, []byte) std::result[[:usize, :[]byte]];
}

struct writer {
    var vtable: *std::writer_vtable;
    var self: *any;

    func init[[:T]](self: *T) std::writer {
        const vtable: std::writer_vtable = (:std::writer_vtable){
            .write = T::write
        };
        return (:std::writer){
            .vtable = &vtable,
            .self = self
        };
    }
}
