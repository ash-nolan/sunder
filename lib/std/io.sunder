namespace std;
import "std/result.sunder";
import "std/writer.sunder";
import "sys/sys.sunder";

struct stream {
    var fd: ssize;

    const STDOUT_FD: ssize = (:ssize)sys::STDOUT_FILENO;
    const STDERR_FD: ssize = (:ssize)sys::STDERR_FILENO;

    func write(self: *stream, bytes: []byte) std::result[[:usize, :[]byte]] {
        if countof(bytes) == 0 {
            return std::result[[:usize, :[]byte]]::init_value(0);
        }

        var sysret: ssize = sys::write((:u32)self.*.fd, &bytes[0u], countof(bytes));
        if sysret < 0 {
            return std::result[[:usize, :[]byte]]::init_error(
                sys::ERRNO_STRINGS[(:usize)-sysret]
            );
        }

        return std::result[[:usize, :[]byte]]::init_value((:usize)sysret);
    }
}

const out: stream = (:stream){.fd = stream::STDOUT_FD};
const err: stream = (:stream){.fd = stream::STDERR_FD};

func print(bytes: []byte) void {
    var writer: std::writer = std::writer::init[[:std::stream]](&std::out);
    var result: std::result[[:usize, :[]byte]] = writer.write(bytes);
    if result.is_error() {
        std::panic(result.error());
    }
}

func println(bytes: []byte) void {
    print(bytes);
    print("\n");
}

func eprint(bytes: []byte) void {
    var writer: std::writer = std::writer::init[[:std::stream]](&std::err);
    var result: std::result[[:usize, :[]byte]] = writer.write(bytes);
    if result.is_error() {
        std::panic(result.error());
    }
}

func eprintln(bytes: []byte) void {
    eprint(bytes);
    eprint("\n");
}
