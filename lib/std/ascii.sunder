# Routines for ASCII bytes and ASCII byte sequences.
namespace std::ascii;

func is_lower(char: byte) bool {
    return 'a' <= char and char <= 'z';
}

func is_upper(char: byte) bool {
    return 'A' <= char and char <= 'Z';
}

func to_upper(char: byte) byte {
    if std::ascii::is_lower(char) {
        return char & 0x5F;
    }
    return char;
}

func is_space(char: byte) bool {
    return char == ' '
        or char == '\t'
        or char == '\n'
        or char == 0x0B  # vertical tab
        or char == 0x0C  # form feed
        or char == 0x0D; # carriage return
}

func trimmed_front(str: []byte) []byte {
    var start = 0u;
    for start < countof(str) and std::ascii::is_space(str[start]) {
        start = start + 1;
    }
    return str[start:countof(str)];
}

func trimmed_back(str: []byte) []byte {
    var end = countof(str);
    for end != 0 and std::ascii::is_space(str[end - 1]) {
        end = end - 1;
    }
    return str[0:end];
}

func trimmed(str: []byte) []byte {
    return std::ascii::trimmed_front(std::ascii::trimmed_back(str));
}
