# Brainfuck interpreter written in Sunder.
#
# See https://esolangs.org/wiki/Brainfuck for an overview of the brainfuck
# language. This implementation uses a 30,000 cell tape with 8-bit wrapping
# cells. No cell update is performed upon reading EOF with the , operation.
import "std";
import "sys";

var cells = (:[30000]u8)[0...];
var cells_index: usize = 0;

func main() void {
    if sys::argc != 2 {
        var progname = std::cstr::to_bytes(*sys::argv);
        var expected = 1u;
        var received = sys::argc - 1u;
        std::print_format_line(
            std::err(),
            "{}: invalid number of arguments (expected {}, received {})",
            (:[]std::formatter)[
                std::formatter::init[[[]byte]](&progname),
                std::formatter::init[[usize]](&expected),
                std::formatter::init[[usize]](&received)]);
        std::exit(std::EXIT_FAILURE);
    }

    var path = std::cstr::to_bytes(*std::ptr::add[[*byte]](sys::argv, 1));
    var result = std::file::open(path, std::OPEN_READ);
    if result.is_error() {
        var error = result.error();
        std::print_format_line(
            std::err(),
            "{}: {}",
            (:[]std::formatter)[
                std::formatter::init[[[]byte]](&path),
                std::formatter::init[[[]byte]](&error)]);
        std::exit(std::EXIT_FAILURE);
    }

    var file = result.value();
    defer {
        file.close();
    }

    var allocator = std::general_allocator::init();
    defer {
        allocator.fini();
    }

    var result = std::read_all(std::reader::init[[std::file]](&file), std::allocator::init[[typeof(allocator)]](&allocator));
    if result.is_error() {
        var error = result.error();
        std::print_format_line(
            std::err(),
            "{}: {}",
            (:[]std::formatter)[
                std::formatter::init[[[]byte]](&path),
                std::formatter::init[[[]byte]](&error)]);
        std::exit(std::EXIT_FAILURE);
    }
    var source = result.value();
    var source_size: usize = countof(source);

    var stack = std::vector[[usize]]::init(std::allocator::init[[typeof(allocator)]](&allocator));
    var source_index: usize = 0;
    for source_index < source_size {
        var cur: byte = source[source_index];

        if cur == '>' {
            cells_index = cells_index + 1;
            source_index = source_index + 1;
            continue;
        }
        if cur == '<' {
            cells_index = cells_index - 1;
            source_index = source_index + 1;
            continue;
        }
        if cur == '+' {
            cells[cells_index] = (:u8)((:ssize)cells[cells_index] + 1);
            source_index = source_index + 1;
            continue;
        }
        if cur == '-' {
            cells[cells_index] = (:u8)((:ssize)cells[cells_index] - 1);
            source_index = source_index + 1;
            continue;
        }
        if cur == '.' {
            std::print(std::out(), (:[]byte){(:*byte)&cells[cells_index], 1});
            source_index = source_index + 1;
            continue;
        }
        if cur == ',' {
            var b: byte = 0;
            var result = std::input_reader::the().*.read((:[]byte){&b, 1});
            if result.value() != 0 { # Only update on non-EOF.
               cells[cells_index] = (:u8)b;
            }
            source_index = source_index + 1;
            continue;
        }
        if cur == '[' {
            if cells[cells_index] == 0 {
                var depth: usize = 1;
                for depth > 0 {
                    source_index = source_index + 1;
                    if source[source_index] == '[' {
                        depth = depth + 1;
                    }
                    if source[source_index] == ']' {
                        depth = depth - 1;
                    }
                }
            } else {
                stack.push(source_index);
            }
            source_index = source_index + 1;
            continue;
        }
        if cur == ']' {
            source_index = stack.pop();
            continue;
        }
        source_index = source_index + 1;
    }
}
