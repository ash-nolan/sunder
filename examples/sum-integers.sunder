# Sum integers from standard input an end-of-file condition is reached.
import "std";

func main() void {
    var reader = std::input();
    var allocator = std::general_allocator::init();
    defer {
        allocator.fini();
    }
    var allocator = std::allocator::init[[typeof(allocator)]](&allocator);

    var sum = std::bigint::init(allocator);
    defer {
        sum.fini();
    }

    for true {
        var result = std::read_line(std::input(), allocator);
        if result.is_error() {
            std::print_line(std::err(), result.error());
            std::exit(std::EXIT_FAILURE);
        }

        var optional = result.value();
        if optional.is_empty() {
            # End-of-input.
            break;
        }
        defer {
            std::slice[[byte]]::delete(allocator, optional.value());
        }

        if countof(optional.value()) == 0 {
            # Skip line consisting of a single '\n'.
            continue;
        }

        var result = std::bigint::init_from_str(allocator, optional.value(), 0);
        if result.is_error() {
            std::print_line(std::err(), result.error());
            std::exit(std::EXIT_FAILURE);
        }
        var int = result.value();
        defer {
            int.fini();
        }

        std::bigint::add(&sum, &sum, &int);
    }

    std::print_format_line(std::out(), "{}", (:[]std::formatter)[std::formatter::init[[std::bigint]](&sum)]);
}
