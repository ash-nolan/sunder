# Simplified (non-POSIX-compliant) version of `ls`.
import "std";
import "sys";

func main() void {
    var progname = std::cstr::data(*sys::argv);
    var path = std::optional[[[]byte]]::init_empty();

    var parser = std::argument_parser::init((:[][]byte)[]);
    for parser.advance() {
        if path.is_value() {
            std::print_format_line(
                std::err(),
                "{}: multiple files specified",
                (:[]std::formatter)[
                    std::formatter::init[[[]byte]](&progname)]);
            std::exit(std::EXIT_FAILURE);
        }
        path = std::optional[[[]byte]]::init_value(parser.argument_value());
    }

    if path.is_empty() {
        path = std::optional[[[]byte]]::init_value(".");
    }

    var result = std::directory::open(path.value());
    if result.is_error() {
        var error = result.error();
        std::print_format_line(
            std::err(),
            "{}: {}",
            (:[]std::formatter)[
                std::formatter::init[[[]byte]](&progname),
                std::formatter::init[[[]byte]](&error)]);
        std::exit(std::EXIT_FAILURE);
    }

    var dir = result.value();
    defer {
        dir.close();
    }

    for dir.advance() {
        var current = dir.current();
        if current.is_error() {
            var error = result.error();
            std::print_format_line(
                std::err(),
                "{}: {}",
                (:[]std::formatter)[
                    std::formatter::init[[[]byte]](&progname),
                    std::formatter::init[[[]byte]](&error)]);
        }

        var current = dir.current();
        var name = current.value();
        std::print_line(std::out(), name);
    }
}
