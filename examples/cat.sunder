import "std/cptr.sunder";
import "std/cstr.sunder";
import "std/io.sunder";
import "sys/sys.sunder";

func cat(filename: *byte) void {
    var sysret: ssize = sys::open(filename, sys::O_RDONLY, 0u16);
    if sysret < 0s {
        std::eprint(std::cstr::to_bytes(filename));
        std::eprintln(": failed to open file");
        sys::exit(1s32);
    }
    var fd: u32 = (:u32)sysret;

    var buf: [512u]byte = (:[512u]byte)[0y...];

    for true {
        sysret = sys::read(fd, &buf[0u], countof(buf));
        if sysret < 0s {
            std::eprint(std::cstr::to_bytes(filename));
            std::eprintln(": read error");
            sys::exit(1s32);
        }

        if sysret == 0s {
            break;
        }

        sysret = sys::write(sys::STDOUT_FILENO, &buf[0u], (:usize)sysret);
        if sysret < 0s {
            std::eprint(std::cstr::to_bytes(filename));
            std::eprintln(": write error");
            sys::exit(1s32);
        }
    }

    sys::close(fd);
}

func main() void {
    for i in 1u:sys::argc {
        var filename: typeof(*sys::argv) = *(:typeof(sys::argv))std::cptr::add(
                (:usize)sys::argv, sizeof(:typeof(sys::argv)), i);
        cat(filename);
    }
}
